import Thermodynamics:
    partial_pressure_vapor, # used as if for conversion from specific humidity to vapor pressure
    saturation_vapor_pressure,
    q_vap_from_RH,
    q_vap_saturation,
    Ice,
    Liquid

"""
Return the number of seconds per day in the given number format.
"""
seconds_per_day(::Type{NF}) where {NF} = ustrip(u"s", NF(1)u"d")

"""
Return the number of seconds per hour in the given number format.
"""
seconds_per_hour(::Type{NF}) where {NF} = ustrip(u"s", NF(1)u"hr")

"""
    $SIGNATURES

Compute partial pressure of oxygen from surface pressure in Pa.
"""
@inline function partial_pressure_O2(pres::NF) where {NF}
    # TODO Shouldn't this be in physical constants?
    pres_O2 = NF(0.209) * pres
    return pres_O2
end

"""
    $SIGNATURES

Compute partial pressure of CO2 from surface pressure and CO2 concentration in Pa.
"""
@inline function partial_pressure_CO2(pres::NF, conc_co2::NF) where {NF}
    pres_co2 = conc_co2 * NF(1.0e-6) * pres
    return pres_co2
end

"""
    relative_to_specific_humidity(r_h, pr, T, c::ThermodynamicConstants)

Derives specific humidity from measured relative humidity `r_h` [%], air pressure `pr` [Pa],
air temperature `T` [°C], and physical constants `c`. Assumes saturation over ice for
`T <= 0°C` and over liquid water otherwise. Wrapper around
[`q_vap_from_RH`](@extref Thermodynamics.q_vap_from_RH).
"""
@inline function relative_to_specific_humidity(r_h, pr, T, c::ThermodynamicConstants)
    T_K = celsius_to_kelvin(c, T)
    phase = T <= zero(T) ? Ice() : Liquid()
    return q_vap_from_RH(c, pr, T_K, r_h / 100, phase)
end

"""
    vapor_pressure_to_specific_humidity(c::ThermodynamicConstants, e, pr)

Derives specific humidity from measured vapor pressure `e` [Pa] and air pressure `pr` [Pa]. 
"""
@inline function vapor_pressure_to_specific_humidity(c::ThermodynamicConstants, e, pr)
    return ε(c) * e / (pr - e * (1 - ε(c)))
end

"""
    saturation_vapor_pressure(T)

Saturation vapor pressure of an air parcel at the given temperature `T` in °C. By default, the saturation vapor
pressure is computed over ice for `T <= 0°C` and over water for `T > 0°C`. Wrapper around
[`saturation_vapor_pressure`](@extref Thermodynamics.saturation_vapor_pressure).
"""
@inline function saturation_vapor_pressure(c::ThermodynamicConstants, T::NF) where {NF}
    T_K = celsius_to_kelvin(c, T)
    return ifelse(
        T <= zero(T),
        saturation_vapor_pressure(c, T_K, Ice()),
        saturation_vapor_pressure(c, T_K, Liquid())
    )
end

"""
    saturation_specific_humidity_vapor(c::ThermodynamicConstants, T, ρ)

Saturation specific humidity at temperature `T` [°C] and density `ρ` [kg/m³]. Dispatches
over ice for `T <= 0°C` and over liquid water otherwise. Wrapper around
[`q_vap_saturation`](@extref Thermodynamics.q_vap_saturation).
"""
@inline function saturation_specific_humidity_vapor(c::ThermodynamicConstants, T, ρ)
    T_K = celsius_to_kelvin(c, T)
    return ifelse(
        T <= zero(T),
        q_vap_saturation(c, T_K, ρ, Ice()),
        q_vap_saturation(c, T_K, ρ, Liquid())
    )
end
