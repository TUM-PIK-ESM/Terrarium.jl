"""
    $TYPEDEF

Coupled process type that encapsulates the coupling of soil energy, water, and carbon dynamics.
The stratigraphy parameterization determines how the vertical layering of the soil is parameterized.
"""
struct SoilEnergyWaterCarbon{
        NF,
        Stratigraphy <: AbstractStratigraphy{NF},
        Energy <: AbstractSoilThermodynamics{NF},
        Hydrology <: AbstractSoilHydrology{NF},
        Biogeochemistry <: AbstractSoilBiogeochemistry{NF},
    } <: AbstractSoil{NF}
    "Soil stratigraphy parameterization"
    strat::Stratigraphy

    "Soil energy balance process"
    energy::Energy

    "Soil hydrology (water balance) process"
    hydrology::Hydrology

    "Soil biogeochemistry process"
    biogeochem::Biogeochemistry
end

function SoilEnergyWaterCarbon(
        ::Type{NF};
        strat = HomogeneousSoilStratigraphy(NF),
        energy = SoilThermodynamics(NF),
        hydrology = SoilHydrology(NF),
        biogeochem = ConstantSoilCarbonDensity(NF)
    ) where {NF}
    return SoilEnergyWaterCarbon(strat, energy, hydrology, biogeochem)
end

# Process interface methods

"""
    $TYPEDSIGNATURES

Initialize the soil energy, water, and carbon state variables on `grid` given
the parameter values in `constants`.
"""
function initialize!(
        state, grid,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants
    )
    initialize!(state, grid, soil.hydrology, soil, constants)
    initialize!(state, grid, soil.biogeochem, soil, constants)
    initialize!(state, grid, soil.energy, soil, constants)
    return nothing
end

"""
    $TYPEDSIGNATURES

Compute auxiliary variables for soil energy, water, and carbon state variables
on `grid` based on the given values in `constants`.

A single fused kernel diagnoses the soil auxiliaries; the per-process `compute_auxiliary!` methods
remain for standalone use and testing.
"""
function compute_auxiliary!(
        state, grid,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants
    )
    out = auxiliary_fields(state, soil)
    fields = get_fields(state, soil)    # full fields, no `except` (see fused-kernel convention)
    launch!(grid, XYZ, compute_auxiliary_kernel!, out, fields, soil)
    return nothing
end

"""
    $TYPEDSIGNATURES

Compute boundary conditions (and halo regions) for soil energy and hydrology.
"""
function compute_boundary_conditions!(state, grid, soil::SoilEnergyWaterCarbon)
    compute_boundary_conditions!(state, grid, soil.hydrology)
    compute_boundary_conditions!(state, grid, soil.energy)
    return nothing
end

"""
    $TYPEDSIGNATURES

Compute tendencies for soil energy, water, and carbon state variables on `grid`
based on the given values in `constants`.

A single fused kernel advances both the soil water (saturation) and energy tendencies; the per-process
`compute_tendencies!` methods remain for standalone use, testing, and Enzyme differentiability.
"""
function compute_tendencies!(
        state, grid,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants,
        evtr::Optional{AbstractEvapotranspiration} = nothing,
    )
    tendencies = tendency_fields(state, soil)
    fields = get_fields(state, soil, evtr)
    launch!(grid, XYZ, compute_tendencies_kernel!, tendencies, state.clock, fields, soil, constants, evtr)
    return nothing
end

# Closures

"""
    $TYPEDSIGNATURES

Compute the forward closure mapping for soil hydrology and energy, in that order.

An optional `runoff` process may be supplied (by the coupled `LandModel`) so that excess water
removed from an oversaturated soil surface is routed into the runoff-owned `surface_excess_water`
pool. Without it (standalone soil), the excess is discarded.
"""
function closure!(
        state, grid,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants,
        runoff::Optional{AbstractSurfaceRunoff} = nothing
    )
    closure!(state, grid, get_closure(soil.hydrology), soil.hydrology, soil, runoff)
    closure!(state, grid, get_closure(soil.energy), soil.energy, soil, constants)
    return nothing
end

"""
    $TYPEDSIGNATURES

Compute the inverse closure mapping for soil hydrology and energy, in that order.

See [`closure!`](@ref) for the role of the optional `runoff` process.
"""
function invclosure!(
        state, grid,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants,
        runoff::Optional{AbstractSurfaceRunoff} = nothing
    )
    invclosure!(state, grid, get_closure(soil.hydrology), soil.hydrology, soil, runoff)
    invclosure!(state, grid, get_closure(soil.energy), soil.energy, soil, constants)
    return nothing
end

# Fused kernels

"""
    $TYPEDSIGNATURES

Fused kernel that advances the coupled soil water and energy tendencies in a single launch. Each
sub-process contributes through its per-cell mutating variant (`compute_water_tendencies!`,
`compute_energy_tendencies!`), which is a no-op for processes with nothing to compute (e.g. `NoFlow`).
"""
@kernel inbounds = true function compute_tendencies_kernel!(
        tendencies, grid, clock, fields,
        soil::SoilEnergyWaterCarbon,
        constants::PhysicalConstants,
        evtr::Optional{AbstractEvapotranspiration},
    )
    i, j, k = @index(Global, NTuple)
    compute_water_tendencies!(tendencies, i, j, k, grid, clock, fields, soil.hydrology, soil.strat, soil.biogeochem, constants, evtr)
    compute_energy_tendencies!(tendencies, i, j, k, grid, fields, soil.energy, soil.hydrology, soil.strat, soil.biogeochem)
end

"""
    $TYPEDSIGNATURES

Fused kernel that diagnoses the coupled soil auxiliaries in a single launch. Each sub-process
contributes through its per-cell mutating variant (`compute_hydraulics!`), which is a no-op for
processes with nothing to diagnose (e.g. `NoFlow`).
"""
@kernel inbounds = true function compute_auxiliary_kernel!(
        out, grid, fields,
        soil::SoilEnergyWaterCarbon
    )
    i, j, k = @index(Global, NTuple)
    compute_hydraulics!(out, i, j, k, grid, fields, soil.hydrology, soil.strat, soil.biogeochem)
end
